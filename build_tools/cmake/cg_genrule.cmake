# Copied from https://github.com/google/iree/blob/main/build_tools/cmake/cg_cc_library.cmake
# Copyright 2019 The IREE Authors
#
# Licensed under the Apache License v2.0 with LLVM Exceptions.
# See https://llvm.org/LICENSE.txt for license information.
# SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

include_guard(GLOBAL)

include(CMakeParseArguments)
include(cg_macros)

# cg_genrule()
#
# CMake function to imitate Bazel's genrule rule.
#
# Parameters:
# TODO(kyleherndon)
# NAME: name of target (see Note)
# SRCS: List of source files for the library
# OUTS: Output files generated by this rule
# CMD: Command to run
# PUBLIC: Add this so that this library will be exported under iree::
# Also in IDE, target will appear in IREE folder while non PUBLIC will be in IREE/internal.
# TESTONLY: When added, this target will only be built if user passes -DIREE_BUILD_TESTS=ON to CMake.
#
# Note:
# By default, cg_genrule will always create a library named cg_${NAME},
# and alias target iree::${NAME}. The iree:: form should always be used.
# This is to reduce namespace pollution.
#
function(cg_genrule)
  cmake_parse_arguments(
    _RULE
    "PUBLIC;TESTONLY"
    "NAME;COMMAND"
    "SRCS;OUTS;DEPENDS;ABS_DEPENDS"
    ${ARGN}
  )

  if(_RULE_TESTONLY AND NOT COMPILER_GYM_BUILD_TESTS)
    return()
  endif()

  # TODO(boian): remove this renaming when call sites do not include ":" in target dependency names
  rename_bazel_targets(_DEPS "${_RULE_DEPENDS}")

  rename_bazel_targets(_NAME "${_RULE_NAME}")

  make_paths_absolute(
    PATHS ${_RULE_SRCS}
    BASE_DIR "${CMAKE_CURRENT_SOURCE_DIR}"
    RESULT_VARIABLE _SRCS
  )

  make_paths_absolute(
    PATHS ${_RULE_OUTS}
    BASE_DIR "${CMAKE_CURRENT_BINARY_DIR}"
    RESULT_VARIABLE _OUTS
  )

  list(LENGTH _OUTS _OUTS_LENGTH)
  if(_OUTS_LENGTH EQUAL 1)
    get_filename_component(_OUTS_DIR "${_OUTS}" DIRECTORY)
  else()
    set(_OUTS_DIR "${CMAKE_CURRENT_BINARY_DIR}")
  endif()

  # Substitute special Bazel references
  string(REPLACE  "$@" "${_OUTS}" _CMD "${_RULE_COMMAND}")
  string(REPLACE  "$(@D)" "${_OUTS_DIR}" _CMD "${_CMD}")
  #string(REPLACE  "$<" "\"${_SRCS}\"" _CMD "${_CMD}")

  add_custom_command(
    OUTPUT ${_OUTS}
    COMMAND bash -c "${_CMD}"
    DEPENDS ${_DEPS} ${_SRCS}
    VERBATIM
  )

  add_custom_target(${_NAME} ALL DEPENDS ${_OUTS})
  set_target_properties(${_NAME} PROPERTIES
    OUTPUTS "${_OUTS}")

  list(LENGTH _OUTS _OUTS_LENGTH)
  if(_OUTS_LENGTH EQUAL "1")
    set_target_properties(${_NAME} PROPERTIES LOCATION "${_OUTS}")
  endif()

endfunction()
